case x {
	default := TRUE
	push { |ys...|; for y in ys if [ x =~ y ] do rval := default; default = FALSE; return rval; done; return FALSE }
	push { return default }
}

help f {
	f "usage: bilar <command> [<args>]"
	f "\ngeneral commands:"
	f "   autoremove - remove packages that are no longer needed"
	f "   compress - make a new package from a source code folder"
	f "   get - download and install a bil package from remote repository"
	f "   info - display information about an installed package"
	f "   install - install a bil package"
	f "   update - update remote package index"
	f "\nmeta commands:"
	f "   help - display this help text"
	f "   version -  display the version number"
	f ""
	f "Type `bilar <command> --help` to get help about a command."
}

main path, args... {
	BILAR_PATH := path
	
	localImport(path.."/bilar.röd")
	bil_database := loadBilDatabase()
	
	localImport(path.."/install.röd")
	localImport(path.."/compress.röd")
	
	if [ #args = 0 ] do
		help { |x...|; errprint *x, "\n" }
		return
	done
	
	cmd = args[0]
	args = args[1:]
	
	case(cmd) | pull(itIs, somethingElse)
	if itIs("help") do
		help print
		return
	done
	if itIs("version") do
		print "Bil Archive v" .. BILAR_VERSION
		return
	done
	if itIs("info") do
		pname := ""
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar info <package-name>"
				return
			done
			if somethingElse do
				pname = args[0]
				break
			done
			shift args
		done
		unless [ bil_database[pname]? ] do
			print "No version of '", pname, "' is installed."
		else
			latest_bil := bil_database[pname][0]
			print "== Latest version =="
			print "Name: ", latest_bil.name
			print "Version: ", latest_bil.version
			manifest := readLines(latest_bil.path.."/manifest.pmf") | parsePamflett()
			if [ manifest["description"]? ] do
				print "Description:"
				print manifest["description"]
			done
			if [ manifest["tags"]? ] do
				tags := manifest["tags"]
				tags = tags&", " if [ tags is list ]
				print "Tags: ", tags
			done
			if [ manifest["dependencies"]? ] do
				print "Dependencies: ", [for d in manifest["dependencies"] do
					if [ d is string ] do
						push d.." (any version)"
					done
					if [ d is map ] do
						push d["name"].." ("..[{
							push "="..d["version"] if [ d["version"]? ]
							push "<="..d["max_version"] if [ d["max_version"]? ]
							push ">="..d["min_version"] if [ d["min_version"]? ]
						}()]&", "..")"
					done
				done]&", "
			done
			print "== Installed versions =="
			for package in bil_database[pname] do
				print package.name, ".", package.version
			done
		done
		available_versions := [searchBilFromIndex(pname)]
		if [ #available_versions > 0 ] do
			print "== Available versions =="
			for package in available_versions do
				print package
			done
		done
		return
	done
	if itIs("install") do
		file := ""
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar install <file>"
				return
			done
			if somethingElse do
				file = args[0]
				break
			done
			shift args
		done
		if installBil(file) do
			print "Package is installed."
		else
			print "Installation failed."
		done
		return
	done
	if itIs("get") do
		file := ""
		version := ""
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar get [-v|--version <version>] <package-name>"
				return
			done
			if itIs("-v", "--version") do
				shift args
				version = args[0]
			done
			if somethingElse do
				file = args[0]
				break
			done
			shift args
		done
		if installBilFromInternet(file, version=version) do
			print "Package is installed."
		else
			print "Installation failed."
		done
		return
	done
	if itIs("update") do
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar update"
				return
			done
			shift args
		done
		if updateIndex() do
			print "Done updating the index."
		else
			print "Failed to update the index."
		done
		return
	done
	if itIs("autoremove") do
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar autoremove"
				return
			done
			shift args
		done
		if autoremove() do
			print "Done removing."
		else
			print "Failed to autoremove."
		done
		return
	done
	if itIs("remove") do
		packages := []
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar remove <packages>"
			done
			if somethingElse do
				packages += args[0]
			done
			shift args
		done
		if removeBilPackage(name) for name in packages do
			print "Done removing."
		else
			print "Failed to remove all packages."
		done
	done
	if itIs("compress") do
		file := ""
		while [ #args > 0 ] do
			case(args[0]) | pull(itIs, somethingElse)
			if itIs("-h", "--help") do
				print "usage: bilar compress <folder>"
				return
			done
			if somethingElse do
				file = args[0]
				break
			done
			shift args
		done
		if createBilPackage(file) do
			print "Package successfully created."
		else
			print "Package creation failed."
		done
		return
	done
	if somethingElse do
		help { |x...|; errprint *x, "\n" }
	done
}
